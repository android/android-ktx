buildscript {
    ext.versions = [
        'kotlin': '1.2.10'
    ]

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.dokka'

repositories {
    jcenter()
    google()
}

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 14

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
}

configurations {
    ktlint
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
    implementation 'com.android.support:support-annotations:27.0.2'

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support:support-annotations:27.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test:rules:1.0.1'
    androidTestImplementation 'com.google.truth:truth:0.37'

    ktlint 'com.github.shyiko:ktlint:0.14.0'
}

task ktlint(type: JavaExec, group: LifecycleBasePlugin.VERIFICATION_GROUP) {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = 'com.github.shyiko.ktlint.Main'
    args 'src/**/*.kt'
}
check.dependsOn ktlint

task ktFormat(type: JavaExec, group: 'formatting') {
    description = 'Fix Kotlin code style deviations.'
    classpath = configurations.ktlint
    main = 'com.github.shyiko.ktlint.Main'
    args '-F', 'src/**/*.kt'
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/documentation"
}
